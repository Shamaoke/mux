#! /usr/bin/env zsh

#
# ::: Utility :::
#

function any; [[ ${${@:2}[(i)$1]} -le ${#@:2} ]]

function each; foreach e (${@:2}) { ${@:1:1} $e }

#
# ::: Lib :::
#

function load_layout {
  local mux_layout=$1
  local mux_state=$2
  local mux_session_name=$3

  . $MUX_LAYOUTS/${mux_layout}.mux $mux_session_name $mux_state
}

function print_layouts {
  local -a layouts=($@)

  f() { printf "%s\n" $1 }

  each f $layouts
}

function start_session {
  local mux_layout=$1
  local mux_state=$2
  local mux_session_name=$3
  local -a mux_layouts=($MUX_LAYOUTS/*.mux)

  f() { print ${1:r:t} }

  local -a basenames=($(each f $mux_layouts))

  { any $mux_layout $basenames } \
    && load_layout $mux_layout $mux_state $mux_session_name \
    || print_layouts $basenames
}

function print_mux_commands {
  local -a commands=('start')

  f() { printf "%s\n" $1 }

  each f $commands
}

#
# ::: Main :::
#

function main {
  local -a args=($@)

  local mux_command=${args[1]}
  local mux_layout=${args[-1]}
  local mux_cwd=${$(pwd):t}

  local -a mux_options=(${args:1:-1})

  local mux_state=${${mux_options[(r)-d|--detached]:+detached}:-attached}
  local mux_session_name=${mux_options[mux_options[(i)-n|--name] + 1]:-${${mux_options[(r)--name=*]#--name=}:-${${mux_cwd}//./_}}}

  [[ $mux_command == 'start' ]] \
    && start_session $mux_layout $mux_state $mux_session_name \
    || print_mux_commands
}

main $@

# vim: set textwidth=80 colorcolumn=80:
