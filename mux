#! /usr/bin/env zsh

#
# ::: Utility :::
#

function any; [[ ${${@:2}[(i)$1]} -le ${#@:2} ]]

function each; foreach e (${@:2}) { ${@:1:1} $e }

#
# ::: Lib :::
#

function load_layout {
  local mux_layout=$1
  local mux_session_name=$2

  . $MUX_LAYOUTS/${mux_layout}.mux $mux_session_name
}

function print_layouts {
  local -a layouts=($@)

  f() { printf "%s\n" $1 }

  each f $layouts
}

function start_session {
  local mux_layout=$1
  local mux_session_name=$2
  local -a mux_layouts=($MUX_LAYOUTS/*.mux)

  f() { print ${1:r:t} }

  local -a basenames=($(each f $mux_layouts))

  { any $mux_layout $basenames } \
    && load_layout $mux_layout $mux_session_name \
    || print_layouts $basenames
}

function print_mux_commands {
  local -a commands=('start')

  f() { printf "%s\n" $1 }

  each f $commands
}

#
# ::: Main :::
#

function main {
  local mux_command=$1
  local mux_layout=$2
  local mux_cwd=${$(pwd):t}
  local mux_session_name=${${3:-${mux_cwd}}//./_}
  #
  # The `mux_session_name` is either the third function argument `$3` or the
  # base name of the current working directory `${$(pwd):t}` with all dots
  # in its name replaced with underscores `${mux_session_name//./_}`.
  #
  # Details
  # -------
  # [14.3 Parameter Expansion](https://zsh.sourceforge.io/Doc/Release/Expansion.html#Parameter-Expansion)
  #

  [[ $mux_command == 'start' ]] \
    && start_session $mux_layout $mux_session_name \
    || print_mux_commands
}

main $@

# vim: set textwidth=80 colorcolumn=80:
